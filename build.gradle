plugins {
  id 'org.springframework.boot' version '2.3.1.RELEASE'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'java'
}

allprojects {
  apply plugin: 'idea'
  apply plugin: 'java-library'
  apply plugin: 'io.spring.dependency-management'

  apply from: rootProject.file('versions.gradle')

  dependencyManagement {
    imports {
      mavenBom("org.springframework.boot:spring-boot-dependencies:2.3.1.RELEASE")
    }
  }

  tasks.withType(JavaCompile) {
    sourceCompatibility = 11
    targetCompatibility = 11
    options.encoding = 'UTF-8'
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    implementation group: 'org.jetbrains', name: 'annotations', version: versions['jetbrains']

    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
      exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }

  all {
    implementation {
      exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    testImplementation {
      exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
  }
}

dependencies {
  implementation project(':interactor')

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  runtimeOnly group: 'com.lmax', name: 'disruptor', version: versions['disruptor']

  implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions['swagger']
  implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: versions['swagger']

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-openfeign-core', version: versions['openfeign-starter']
}

test {
  useJUnitPlatform()
}

bootBuildImage {
  imageName = "iamhostage/${project.name}:${project.version}"
}

task bootPushImage {
  group = 'build'
  description = 'Pushes an OCI image of the application using the output of bootBuildImage task'
  dependsOn bootBuildImage

  doLast {
    exec {
      executable 'docker'
      args 'push', "docker.io/${bootBuildImage.imageName}"
    }
  }
}
