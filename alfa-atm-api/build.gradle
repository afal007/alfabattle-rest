plugins {
  id 'maven-publish'
  id 'org.openapi.generator' version '4.3.1'
}

dependencies {
  api group: 'io.springfox', name: 'springfox-swagger2', version: '2.8.0'

  api group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
  api group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

  api group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

def spec = "$projectDir/docs/openapi/atm.yaml"
def generatedSourcesDir = "$buildDir/generated/openapi"

version = '0.0.1'

openApiGenerate {
  generatorName.set('java')

  inputSpec.set(spec)
  outputDir.set(generatedSourcesDir)

  apiPackage.set('ru.alfabank.api')
  modelPackage.set('ru.alfabank.model')
  invokerPackage.set('ru.alfabank.invoker')

  configOptions.set([dateLibrary: 'java8', serializationLibrary: 'jackson', library: 'feign'])
}

task openApiGenerateAndCleanup {
  group 'openapi tools'
  dependsOn tasks.openApiGenerate
  doLast {
    copy {
      def generatedSourcesRoot = "$buildDir/generated/sources/java/main"
      mkdir generatedSourcesRoot
      from "$buildDir/generated/openapi/src/main/java"
      into generatedSourcesRoot
      exclude "**/api/**"
      exclude "**/invoker/**"
    }
    delete "$buildDir/generated/openapi"
  }
}

tasks {
  compileJava {
    dependsOn openApiGenerateAndCleanup
  }
}
